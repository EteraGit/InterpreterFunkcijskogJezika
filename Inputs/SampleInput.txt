add(x,0):=x
add(x,y+1):=Sc(add(x,y))

pd(0):=0
pd(y+1):=y

sub(x,0):=x
sub(x,y+1):=pd(sub(x,y))

// zakomentirano za sad (x+y) ::= add(x,y)   // nice to have

f(x,y):=add(Sc(Sc(x)),y)

g(x,y):=add(sub(x,y), y)

h(x,y,z):=mul(Sc(z), sub(x,y))

mul(x,0):=0
mul(x,y+1):=add(x,mul(x,y))

factorial(0):=1
factorial(n+1):=mul(Sc(n),factorial(n))

[Positive](0):=0
[Positive](x+1):=1

{Greater}(x,y):<=>{Positive}(sub(x,y))  // za x = y ne radi kako treba

div(x,y):=pd((mu z){Greater}(mul(z,y),x))

mod(x,y):=sub(x,mul(y,div(x,y)))   // (x-(y*(x//y)))

{Equal}(x,y):<=>!{Greater}(x,y) && !{Greater}(y,x)

{Divides}(x,y):<=>{Equal}(mod(y,x),Z(x))  // moze i Equal(mod(x,y),0)

{Prime}(p):<=>{Equal}((#d<=p){Divides}(d,p), 2)

nextprime(p):=(mu q)({Prime}(q) && {Greater}(q,p))  // eventualno AND(...)


// OgraniÄeno: (keyword x <(=) izraz)relacija

add(10, 2) // 12

sub(10, 2) // 8

sub(5, 5)  // 0

sub(0, 0)  // 0

sub(1, 2)  // 0

pd(Sc(4))  // 4

mul(2, 3)  // 6

mul(add(2,3), sub(5, 2))  // 15

mul(mul(2,2), 4)  // 16

factorial(5)  // 120

mul(factorial(4), 4)  // 96

f(2,3)  // 7

g(2,3)  // 3

h(10,3,4)  // 35

Positive(0)  // 0
Positive(10)  // 1
Positive(sub(4,4))  // 0

Greater(10, 2)  // 1
Greater(2, 10)  // 0
Greater(2, 2)  // 0

div(10, 2)  // 5
div(0, 2)  // 0
div(15, 4)  // 3

mod(20, 2)  // 0
mod(20, 3)  // 2
mod(16, 7)  // 2

Equal(10, 10)   // 1
Equal(10, 11)   // 0
Equal(10, 9)    // 0

Divides(2, 10)  // 1
Divides(3, 10)  // 0

Prime(2)    // 1
Prime(3)    // 1
Prime(4)    // 0
Prime(5)    // 1
Prime(6)    // 0
Prime(7)    // 1

nextprime(2)    // 3
nextprime(3)    // 5
nextprime(4)    // 5
nextprime(5)    // 7
nextprime(6)    // 7
nextprime(7)    // 11


